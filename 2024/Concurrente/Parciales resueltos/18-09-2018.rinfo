programa parcial
procesos
  proceso identificarRobots
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    EnviarMensaje(4,r4)
  fin
  proceso irAvCa(E id:numero; E p:boolean; E av:numero; E ca:numero; E avO:numero; E caO:numero)
  comenzar
    BloquearEsquina(av,ca)
    Pos(av,ca)
    mientras(HayFlorEnLaEsquina)
      EnviarMensaje(id,rJ)
      EnviarMensaje(p,rJ)
      tomarFlor
    mientras(HayPapelEnLaEsquina)
      EnviarMensaje(id,rJ)
      EnviarMensaje(p,rJ)
      tomarPapel
    Pos(avO,caO)
    LiberarEsquina(av,ca)
  fin
  proceso finIntentos(E p:boolean; E id:numero)
  comenzar
    p:=F
    EnviarMensaje(id,rJ)
    EnviarMensaje(F,rJ)
  fin
  proceso depositarGanador(E rG:numero; E id:numero)
  comenzar
    si(rG=id)
      mientras(HayFlorEnLaBolsa)
        depositarFlor
      mientras(HayPapelEnLaBolsa)
        depositarPapel
  fin
  proceso calcularGanador(E fp1:numero; E fp2:numero; E fp3:numero; E fp4:numero; ES rG:numero)
  variables
    fpMax:numero
  comenzar
    fpMax:=-1
    si(fp1>fpMax)
      fpMax:=fp1
      rG:=1
    si(fp2>fpMax)
      fpMax:=fp2
      rG:=2
    si(fp3>fpMax)
      fpMax:=fp3
      rG:=3
    si(fp4>fpMax)
      fpMax:=fp4
      rG:=4
  fin
areas
  a1: AreaP(10,10,10,10)
  a2: AreaP(11,10,11,10)
  a3: AreaP(12,10,12,10)
  a4: AreaP(13,10,13,10)
  aJ: AreaP(1,1,1,1)
  aC: AreaPC(45,48,62,69)
robots
  robot recolector
  variables
    avOrigin,caOrigin,av,ca,rGanador,id:numero
    program:boolean
  comenzar
    RecibirMensaje(id,rJ)
    avOrigin:=PosAv
    caOrigin:=PosCa
    program:=V
    repetir 5
      Random(av,45,62)
      Random(ca,48,69)
      irAvCa(id,program,av,ca,avOrigin,caOrigin)
    finIntentos(program,id)
    RecibirMensaje(rGanador,rJ)
    depositarGanador(rGanador,id)
  fin
  robot jefe
  variables
    id,rGanador,fp1,fp2,fp3,fp4:numero
    program1,program2,program3,program4:boolean
  comenzar
    fp1:=0
    fp2:=0
    fp3:=0
    fp4:=0
    program1:=V
    program2:=V
    program3:=V
    program4:=V
    identificarRobots
    mientras(program1)|(program2)|(program3)|(program4)
      RecibirMensaje(id,*)
      si(id=1)
        RecibirMensaje(program1,r1)
        si(program1)
          fp1:=fp1+1
      sino
        si(id=2)
          RecibirMensaje(program2,r2)
          si(program2)
            fp2:=fp2+1
        sino
          si(id=3)
            RecibirMensaje(program3,r3)
            si(program3)
              fp3:=fp3+1
          sino
            RecibirMensaje(program4,r4)
            si(program4)
              fp4:=fp4+1

    calcularGanador(fp1,fp2,fp3,fp4,rGanador)
    Informar(fp1)
    Informar(fp2)
    Informar(fp3)
    Informar(fp4)
    EnviarMensaje(rGanador,r1)
    EnviarMensaje(rGanador,r2)
    EnviarMensaje(rGanador,r3)
    EnviarMensaje(rGanador,r4)
    Informar(rGanador)
  fin
variables
  r1,r2,r3,r4:recolector
  rJ:jefe
comenzar
  AsignarArea(r1,a1)
  AsignarArea(r1,aC)
  AsignarArea(r2,a2)
  AsignarArea(r2,aC)
  AsignarArea(r3,a3)
  AsignarArea(r3,aC)
  AsignarArea(r4,a4)
  AsignarArea(r4,aC)
  AsignarArea(rJ,aJ)
  Iniciar(r1,10,10)
  Iniciar(r2,11,10)
  Iniciar(r3,12,10)
  Iniciar(r4,13,10)
  Iniciar(rJ,1,1)
fin
